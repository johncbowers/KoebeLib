plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.10'
    id 'application'
}

// Set the main class for application and jar building
//  - set in settings.gradle or 
//    defined with -Pmain=<class> in build command
mainClassName = project.findProperty('main') ?: gradle.ext.defaultClassName

// Set default source path
sourceSets {
    main.kotlin.srcDirs += '../src/'
}

// Use maven central repository for dependencies
repositories {
    mavenCentral()
}

dependencies {
    // Kotlin standard library
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    // KoebeLib dependencies
    compile files('../lib/jar/cpcore.jar')
    compile 'org.antlr:antlr4:4.7.1'
    compile 'org.python:jython-standalone:2.7.0'
    compile 'com.fifesoft:rsyntaxtextarea:2.6.1'
    compile 'org.jogamp.jogl:jogl-all-main:2.3.2'
    compile 'org.jogamp.gluegen:gluegen-rt-main:2.3.2'
    compile ('org.processing:core:3.3.6') {
        transitive = false
    }
}

// Use lib gradle for downloading CirclePack cpcore.jar
task get(type:GradleBuild) {
    setDir '../lib'
    buildFile  = '../lib/build.gradle'
    tasks = ['get']
}

// Can't compile without cpcore.jar
compileKotlin.dependsOn(get)

// Build jar with main class defined earlier
// Bundle all dependencies in the jar
jar {
    manifest {
        attributes 'Main-Class' : project['mainClassName']
    }

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}
